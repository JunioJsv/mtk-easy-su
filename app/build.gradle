apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.google.firebase.crashlytics'

final Properties env = new Properties()
file("${rootDir}/env.properties").withInputStream { env.load(it) }

android {

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "mtk-easy-su-v${defaultConfig.versionName}.apk"
        }
    }

    signingConfigs {
        release {
            storeFile file("${rootDir}/key.jks")
            storePassword env['key.password']
            keyPassword env['key.password']
            keyAlias env['key.alias']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId 'juniojsv.mtk.easy.su'
        minSdkVersion 23
        compileSdk 34
        versionCode 210
        versionName "2.2.1"
    }

    buildTypes {
        configureEach {
            resValue "string", "application_advertising_id", env['application.advertising.id']
            resValue "string", "interstitial_advertising_id", env['interstitial.advertising.id']
            resValue "string", "banner_advertising_id", env['banner.advertising.id']
            resValue "string", "admob_test_device", env['admob.test.device']
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    namespace 'juniojsv.mtk.easy.su'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.android.gms:play-services-ads:24.1.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation platform('com.google.firebase:firebase-bom:33.11.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}
